# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=linux-rp2
pkgver=3.18.79
pkgrel=7
pkgdesc="RetroRoot retroid pocket 2 kernel"
arch=('armv7h')
license=('GPL')
commit=207233b2af5bdbef7e4f6570829d6aca2be6cb84
url="https://github.com/turtleletortue/android_kernel_retroid_pocket2/archive/$commit.zip"

# every retroroot pkg must be a member of "rr-game", "rr-emulator", "rr-app" or "rr-system" group
groups=('rr-system')

depends=('coreutils' 'mkinitcpio>=0.7')
makedepends=('rr-env' 'rr-toolchain-armv7h')

source=(
  "$url"
  config-retroid-pocket2
  python2-to-python3-compatibility.patch
  gcc8-fix-put-user.patch
)
sha512sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

prepare() {
  _builddir="$srcdir/android_kernel_retroid_pocket2-$commit"
  cd "$_builddir"
  
  # Remove -Werror from all makefiles 
  # ref: https://gitlab.com/postmarketOS/pmaports/-/blob/master/main/devicepkg-dev/downstreamkernel_prepare.sh
  makefiles="$(find "$_builddir" -type f -name Makefile) $(find "$_builddir" -type f -name Makefile.common) $(find "$_builddir" -type f -name Kbuild)"
  for i in $makefiles; do
    sed -i 's/-Werror-/-W/g' "$i"
    sed -i 's/-Werror=/-W/g' "$i"
    sed -i 's/-Werror//g' "$i"
  done
  
  # patches
  patch -Np1 -i ../python2-to-python3-compatibility.patch
  patch -Np1 -i ../gcc8-fix-put-user.patch

  echo "Setting version..."
  mkdir out && cd out
  ../scripts/setlocalversion --save-scmversion
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "${pkgbase#linux}" > localversion.20-pkgname
  
  echo "Setting config..."
  cp "$srcdir/config-retroid-pocket2" .config
  yes "" | make -C "$_builddir" ARCH="arm" O="out" oldconfig
  
  echo "Setting version..."
  source /opt/retroroot/rr-env ${CARCH}
  make V=1 $MAKEFLAGS ARCH="arm" O="out" CROSS_COMPILE=${RETROROOT_CROSS_PREFIX} prepare
  make V=1 $MAKEFLAGS ARCH="arm" O="out" CROSS_COMPILE=${RETROROOT_CROSS_PREFIX} -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  _builddir="$srcdir/android_kernel_retroid_pocket2-$commit"
  cd "$_builddir"
  
  source /opt/retroroot/rr-env ${CARCH}

  make V=1 $MAKEFLAGS ARCH="arm" O="out" CROSS_COMPILE=${RETROROOT_CROSS_PREFIX}
		#KBUILD_BUILD_VERSION="$((pkgrel + 0 ))-retroroot"
}

package() {
  _builddir="$srcdir/android_kernel_retroid_pocket2-$commit"
  cd $_builddir/out
  
  install -d "$pkgdir/boot"
  cp arch/arm/boot/zImage-dtb "$pkgdir/boot/vmlinuz-linux"
}
