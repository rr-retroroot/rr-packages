# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=wesnoth
pkgver=1.16.2
pkgrel=1
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl2_mixer' 'sdl2_image' 'boost' 'pango' 'dbus')
makedepends=('boost' 'cmake' 'git' 'ninja')
options=(!emptydirs)
source=("https://github.com/wesnoth/wesnoth/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('4d02e1cb499c3f5769cf25e6598974903d991ea5a7e5f09ebac86dd47b4c0d0b')

build() {
  cd "${pkgname}-${pkgver}"

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DENABLE_DISPLAY_REVISION=OFF
  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="$pkgdir" ninja -C build install

  # See http://forums.wesnoth.org/viewtopic.php?t=51038
  echo "Linux repository" > "$pkgdir"/usr/share/wesnoth/data/dist
}

