# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=rr-opentyrian
pkgver=2.1.20220318
pkgrel=1
pkgdesc='Open Source port of the classic DOS shoot-em-up Tyrian'
url='https://github.com/opentyrian/opentyrian'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('sdl2' 'sdl2_net')
provides=('tyrian' 'opentyrian')
replaces=('tyrian' 'opentyrian')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz"
        'http://camanis.net/tyrian/tyrian21.zip')
sha256sums=('e0c6afbb5d395c919f9202f4c9b3b4da7bd6e993e9da6152f995012577e1ccbd'
            '7790d09a2a3addcd33c66ef063d5900eb81cc9c342f4807eb8356364dd1d9277')

build() {
	make prefix=/usr all -C "$srcdir/opentyrian-${pkgver}"
}

package() {
	cd "$srcdir/opentyrian-${pkgver}"
	make DESTDIR="$pkgdir" prefix=/usr install

	cd "$srcdir/tyrian21"
	install -Dt "$pkgdir/usr/share/games/tyrian" -m644 *.dat *.lvl *.shp *.snd \
		demo.* music.mus tyrend.anm tyrian.{cdt,hdt,pic} tshp2.pcx
}
